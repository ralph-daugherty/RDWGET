     A*****************************************************************
     A*               RDWGET iSeries Scripted Web Retrieval
     A*  File RDWGETCFG
     A*    ordered SCRIPTKEY
     A*****************************************************************
     A          R GETCFGR                   TEXT('RDWGET script config')
     A            SCRKEY        10A         TEXT('Script key')
     A                                      COLHDG('Key')
     A            SCRPATH      120A         TEXT('Script path')
     A                                      COLHDG('Script' 'path')
     A            SCRSUBDIR     40A         TEXT('Script subdirectory')
     A                                      COLHDG('Script' 'subdir')
     A            SCRFILE       40A         TEXT('Script file')
     A                                      COLHDG('Script' 'file')
     A            WRKPATH      120A         TEXT('Work path')
     A                                      COLHDG('Work' 'path')
     A            WRKSUBDIR     40A         TEXT('Work subdirectory')
     A                                      COLHDG('Work' 'subdir')
     A            WRKFILE       40A         TEXT('Work file')
     A                                      COLHDG('Work' 'file')
     A            WRKMOD        10A         TEXT('Work mod logic')
     A                                      COLHDG('Work' 'mod')
     A            INPPATH      120A         TEXT('Input path')
     A                                      COLHDG('Input' 'path')
     A            INPSUBDIR     40A         TEXT('Input subdirectory')
     A                                      COLHDG('Input' 'subdir')
     A            INPFILE       40A         TEXT('Input file')
     A                                      COLHDG('Input' 'file')
     A            INPMOD        10A         TEXT('Input mod logic')
     A                                      COLHDG('Input' 'mod')
     A            OUTPATH      120A         TEXT('Output path')
     A                                      COLHDG('Output' 'path')
     A            OUTSUBDIR     40A         TEXT('Output subdirectory')
     A                                      COLHDG('Output' 'subdir')
     A            OUTFILE       40A         TEXT('Output file')
     A                                      COLHDG('Output' 'file')
     A            OUTMOD        10A         TEXT('Output mod logic')
     A                                      COLHDG('Output' 'mod')
     A            OUTRET         3  0       TEXT('Days retention')
     A                                      COLHDG('Retain')
     A            POSTPROC      10A         TEXT('Post Process')
     A                                      COLHDG('Post' 'proc')
     A            ERRPROC       10A         TEXT('Error Process')
     A                                      COLHDG('Err' 'proc')
     A*****************************************************************
     A          K SCRKEY
